apply plugin: GetVersion

allprojects {
    version = '1.0.0'
}

subprojects {
    apply plugin: 'java'
    apply plugin: GetVersion

    java {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }
}

wrapper {
    gradleVersion = '7.4.2'
}

task getWar(type: Copy) {
    doFirst {
        println "Copy war-file to project folder ($rootProject)"
    }
    from "web/build/libs"
    into "."
    include "*.war"
}

getWar.dependsOn ':web:build'

class GetVersion implements Plugin<Project> {
    void apply(Project project) {
        def env = 'LOCAL'
        if (project.hasProperty("envDescription") && project.getProperty("envDescription") == 'RELEASE') {
            env = 'RELEASE'
        }
        def props = new Properties()
        new File("gradle.properties").withInputStream { props.load(it) }
        def majorVersion = props.getProperty("majorVersion")
        def minorVersion = props.getProperty("minorVersion")
        project.version = "$majorVersion.$minorVersion-$env"
    }
}